import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.internal.jvm.Jvm

dependencies {
    api project(':logging')

    // <Testerra>
    // @see https://github.com/google/guice/issues/1256
    api 'com.google.inject:guice:4.2.2:no_aop'
    api 'com.google.inject.extensions:guice-assistedinject:4.2.2'
    implementation 'org.reflections:reflections:0.9.12'
    // </Testerra>

    api 'commons-io:commons-io:2.6'
    // StringUtils needs api dep
    api 'org.apache.commons:commons-lang3:3.9'

    // <PdfUtils>
    implementation 'org.apache.pdfbox:pdfbox:2.0.29'
    // </PdfUtils>

    api "com.google.guava:guava:${guavaVersion}"

    // <XLSTestDataReader>
    implementation 'org.apache.poi:poi-ooxml:3.17'
    // </XLSTestDataReader>

    // Selenium 4.6 comes with 'io.netty:netty-transport-native-epoll:4.1.84.Final'
    // -> Dependency error Could not find netty-transport-native-epoll-4.1.84.Final-linux-x86_64.jar (io.netty:netty-transport-native-epoll:4.1.84.Final).
    api 'org.seleniumhq.selenium:selenium-java:' + seleniumVersion

    api('org.testng:testng:7.8.0')

    // <FileUtils>
    implementation 'net.lingala.zip4j:zip4j:1.3.2'
    implementation 'de.idyl:winzipaes:1.0.1'
    // </FileUtils>

}

processResources {
    filesMatching('**/testerra-build.properties') {
        filter ReplaceTokens, tokens: [
                'build.java.version': Jvm.current().javaVersion.toString(),
                'build.os.name'     : System.getProperty("os.name"),
                'build.os.arch'     : System.getProperty("os.arch"),
                'build.os.version'  : System.getProperty("os.version"),
                'build.user.name'   : System.getProperty("user.name"),
                'build.version'     : project.version.toString(),
                'build.timestamp'   : new Date().toString()
        ]
    }
}

test {
    exclude '**'
}
