dependencies {
    implementation project(':integration-tests')

    implementation driver_ui_desktop
    testImplementation project(':report-ng')
}

// Deactivate JAR publishing
project.afterEvaluate {
    println "Deactivate publishing for " + project.name
    project.tasks.each { t ->
        if (t.name.startsWith("publish")) {
            t.enabled = false
        }
    }
}

test {
    String[] s = [
        'src/test/resources/testng/TestReport.xml',
    ]


    def preTest = findProperty("pretestRNG")
    def preTestExtended = findProperty("pretestRNGExtended")
    def playground = findProperty("playground")
    def historyIndex = findProperty("pretestHistoryIndex") as Integer

    if (preTest) {
        s = ['src/test/resources/testng/GenerateReport.xml']
        System.setProperty("tt.report.dir", "src/test/resources/reports/report-generated-status")
        System.setProperty("tt.report.name", "Generated Report for UI-Test")
    }

    if (preTestExtended) {
        s = ['src/test/resources/testng/GenerateExtendedReport.xml']
        System.setProperty("tt.report.dir", "src/test/resources/reports/report-extended-status")
        System.setProperty("tt.report.name", "Generated Report for Additional UI-Test")
    }

    if (historyIndex) {
        if (historyIndex > 5) {
            s = ['src/test/resources/testng/Pretest_HistoryTest_2.xml']
        } else {
            s = ['src/test/resources/testng/Pretest_HistoryTest_1.xml']
        }
    }

    if (playground) {
        s = ['src/test/resources/testng/Playground.xml']
    }

    useTestNG() {
        suites s
//        configFailurePolicy "continue"
    }
    options {
        systemProperties(System.getProperties())
    }
}

task cleanReports {
    doLast {
        delete "test-report"
        delete "target"
        delete "logs"
        for(int nr=0; nr < 10; ++nr) {
            delete "report-testsundertest-0$nr"
        }
    }
}
